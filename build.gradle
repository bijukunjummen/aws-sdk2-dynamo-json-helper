buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'nebula.release' version '17.2.2'
    id "com.github.spotbugs" version "5.0.14"
}

group = 'com.github.bijukunjummen'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

ext {
    jacksonBom = "2.15.0"
    junitVersion = "5.9.3"
    assertJVersion = "3.24.2"
    testContainersVersion = "1.18.0"
    jsonAssertVersion = "1.5.0"
    awsSdkVersion = "2.20.86"
}
repositories {
    mavenCentral()
}

dependencies {
    api platform("com.fasterxml.jackson:jackson-bom:${jacksonBom}")
    api("com.fasterxml.jackson.core:jackson-core")
    api("com.fasterxml.jackson.core:jackson-databind")
    api("software.amazon.awssdk:dynamodb:${awsSdkVersion}")
    testImplementation("org.assertj:assertj-core:$assertJVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testImplementation("org.testcontainers:testcontainers:${testContainersVersion}")
    testImplementation("org.testcontainers:localstack:${testContainersVersion}")
    testImplementation("org.skyscreamer:jsonassert:${jsonAssertVersion}")
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}
ext.isReleaseVersion = !version.toString().endsWith("-SNAPSHOT")

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

tasks.snapshot {
    finalizedBy tasks.publish
}

tasks.candidate {
    finalizedBy tasks.publish
}

tasks.final {
    finalizedBy tasks.publish
}

publishing {
    repositories {
        maven {
            def snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            def releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

            url = isReleaseVersion ? releaseUrl : snapshotUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = 'Utility for converting a JSON to AWS SDK2 AttributeValue and back'
                url = 'https://github.com/bijukunjummen/aws-sdk2-dynamo-json-helper.git'
                inceptionYear = '2021'

                packaging = 'jar'
                groupId = project.group
                artifactId = project.name
                version = project.version

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    connection = 'https://github.com/bijukunjummen/aws-sdk2-dynamo-json-helper.git'
                    url = 'https://github.com/bijukunjummen/aws-sdk2-dynamo-json-helper.git'

                }
                developers {
                    developer {
                        name = 'Biju Kunjummen'
                        id = 'bijukunjummen'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
